@prefix wdt: <http://www.wikidata.org/prop/direct/>.
@prefix wikibase: <http://wikiba.se/ontology#>.

%@import wikidata :- ntriples{resource="wikidata.nt.gz"}.
wikidata("entityA", wdt:P1696, "entityB").
wikidata("entityA", wikibase:directClaim, "a").
wikidata("entityB", wikibase:directClaim, "b").
wikidata("x", "a", "y").
wikidata("x", "a", "z").
wikidata("y", "b", "x").
wikidata("z", "b", "x").

inversePropEntities(?s,?o) :- wikidata(?s,wdt:P1696,?o), COMPARE(STR(?s), STR(?o)) = -1.
inv(?p1, ?p2) :- inversePropEntities(?e1, ?e2), wikidata(?e1, wikibase:directClaim, ?p1), wikidata(?e2, wikibase:directClaim, ?p2).
pred(?s) :- inv(?s,_).
pred(?o) :- inv(_,?o).

objCountPerPredAndSource(?p,?s,#count(?o)) :- 
    pred(?p), wikidata(?s,?p,?o).
totalSubjCountPerPred(?p,#count(?s)) :- 
    objCountPerPredAndSource(?p,?s,_).

avgObjCountPerPredicate(?p,DOUBLE(#sum(?count,?s)) / ?totalSubj) :- 
    objCountPerPredAndSource(?p,?s,?count), 
    totalSubjCountPerPred(?p,?totalSubj).

rightSmaller(?right) :- 
    inv(?left,?right),
    avgObjCountPerPredicate(?left,?countLeft),
    avgObjCountPerPredicate(?right,?countRight),
    ?countRight < ?countLeft.

predToKeep(?right) :- rightSmaller(?right).
predToKeep(?left) :- inv(?left,?right), ~rightSmaller(?right).

@output predToKeep.